plugins {
    id 'java'
    id 'application'
    // https://plugins.gradle.org/plugin/com.gradleup.shadow
    id 'com.gradleup.shadow' version '8.3.5'
    id 'com.diffplug.spotless' version '7.0.2'
    id 'net.ltgt.errorprone' version '4.1.0'
}

group = 'io.github.eisopux'
version = '0.2.0'

repositories {
    mavenCentral()
}

configurations {
    requireJavadoc
}

dependencies {
    implementation 'io.github.eisop:checker:3.42.0-eisop5'
    implementation 'org.eclipse.lsp4j:org.eclipse.lsp4j:0.23.1'
    implementation 'com.google.code.gson:gson:2.11.0'
    implementation 'commons-cli:commons-cli:1.9.0'
    implementation 'com.google.guava:guava:33.4.0-jre'
    testImplementation 'junit:junit:4.13.2'

    if (JavaVersion.current() >= JavaVersion.VERSION_17) {
        errorprone('com.google.errorprone:error_prone_core:2.36.0')
        errorproneJavac('com.google.errorprone:javac:9+181-r4173-1')
    }
    requireJavadoc('org.plumelib:require-javadoc:1.0.9')
}

task requireJavadoc(type: JavaExec, group: 'Documentation') {
    description = 'Ensures that Javadoc documentation exists in source code.'
    mainClass = 'org.plumelib.javadoc.RequireJavadoc'
    classpath = configurations.requireJavadoc
    args 'src/main/java'
}

application {
    // Define the main class for the application
    mainClass = 'org.checkerframework.languageserver.ServerMain'
}

shadowJar {
    minimize()
    dependencies {
        // Minimize does not remove the annotated JDK, properties, and astub files
        exclude(dependency('io.github.eisop:checker'))
    }
}

tasks.withType(JavaCompile) { compilationTask ->
    options.compilerArgs += ['-Xlint', '-Werror']
    options.errorprone.enabled = JavaVersion.current() >= JavaVersion.VERSION_17
}

spotless {
    java {
        googleJavaFormat().aosp()
        importOrder('com', 'jdk', 'lib', 'lombok', 'org', 'java', 'javax')
        formatAnnotations()
    }
    groovyGradle {
        target '*.gradle'
        importOrder()
        greclipse()  // which formatter Spotless should use to format .gradle files.
        indentWithSpaces(4)
        trimTrailingWhitespace()
    }
}
